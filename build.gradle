plugins {
    id 'scala'
}

ext {
    karateVersion = '1.5.1'
}

dependencies {
    testImplementation "io.karatelabs:karate-gatling:${karateVersion}"
}

repositories {
    mavenCentral()
}

test {
    systemProperty "karate.options", System.properties.getProperty("karate.options")
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    systemProperty "rampUsers", System.properties.getProperty("rampUsers")
    systemProperty "during", System.properties.getProperty("during")
    systemProperty "responseTimeMax", System.properties.getProperty("responseTimeMax")
    systemProperty "errorRateMax", System.properties.getProperty("errorRateMax")
    systemProperty "requestsPerSecMin", System.properties.getProperty("requestsPerSecMin")
    systemProperty "successfulRequestsMin", System.properties.getProperty("successfulRequestsMin")
    systemProperty "urlBase", System.properties.getProperty("urlBase")
    
    outputs.upToDateWhen { false }
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/java/resources')
            exclude '**/*.java'
            exclude '**/*.scala'
        }
        scala {
            srcDirs = ['src/test/java']
        }
    }
}

tasks.register('cleanGatlingReports', Delete) {
    delete "${buildDir}/reports/gatling"
}

// to run, type: "gradle gatlingRun"
tasks.register('gatlingRun', JavaExec) {
    group = 'Web Tests'
    description = 'Run Gatling Tests'
    new File("${buildDir}/reports/gatling").mkdirs()
    classpath = sourceSets.test.runtimeClasspath
    main = "io.gatling.app.Gatling"
    args = [
            // change this to suit your simulation entry-point
            '-s', 'performance.PerformanceTestRunner',
            '-rf', "${buildDir}/reports/gatling"
    ]
    systemProperties System.properties
    dependsOn cleanGatlingReports
}