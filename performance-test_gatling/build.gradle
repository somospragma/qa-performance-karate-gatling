plugins {
    id 'scala'
}

ext {
    karateVersion = '1.5.0'
}

dependencies {
    testImplementation "io.karatelabs:karate-gatling:${karateVersion}"
//    testImplementation 'com.intuit.karate:karate-junit5:1.5.0'
}

repositories {
    mavenCentral()
    // mavenLocal()
}

test {
    systemProperty "karate.options", System.properties.getProperty("karate.options")
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    outputs.upToDateWhen { false }
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/java/resources')
            exclude '**/*.java'
            exclude '**/*.scala'
        }
        scala {
            srcDirs = ['src/test/java']
        }
    }
}

task cleanGatlingReports(type: Delete){
    delete "${buildDir}/reports/gatling"
}

// to run, type: "gradle gatling"
task gatlingRun(type: JavaExec) {
    group = 'Web Tests'
    description = 'Run Gatling Tests'
    new File("${buildDir}/reports/gatling").mkdirs()
    classpath = sourceSets.test.runtimeClasspath
    main = "io.gatling.app.Gatling"
    args = [
            // change this to suit your simulation entry-point
            '-s', 'performance.PerformanceTestRunner',
            '-rf', "${buildDir}/reports/gatling"
    ]
    systemProperties System.properties
    dependsOn cleanGatlingReports
}